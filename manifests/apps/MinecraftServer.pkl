import "@k8s/api/apps/v1/Deployment.pkl"
import "@k8s/api/core/v1/ConfigMap.pkl"
import "@k8s/api/core/v1/Namespace.pkl"
import "@k8s/api/core/v1/PersistentVolumeClaim.pkl"

import "../components/McProxy.pkl"
import "../components/McServer.pkl"
import "../components/Velocity.pkl"
import "../components/Workload.pkl"

local mcServer = new McServer {
  eula = true
  type = "FABRIC"
  onlineMode = false
  modrinthProjects {
    "fabricproxy-lite"
    "fabric-api"
  }
  workload {
    name = "vanilla-test"
    namespace = "minecraft"
  }
  env {
    ["FABRIC_PROXY_SECRET"] = mcProxy.workload.name.sha1
  }
}

local mcProxy = new McProxy {
  type = "VELOCITY"
  hostname = "lucsoft.de"
  velocity {
    motd = "A Minecraft Server"
    showMaxPlayers = 20
    playerInfoForwardingMode = "MODERN"
    servers {
      list {
        ["lobby"] = mcServer.address
      }
      try {
        "lobby"
      }
    }
  }
  workload {
    namespace = "minecraft"
    deployment {
      spec {
        strategy {
          type = "Recreate"
        }
      }
    }
    spec {
      containers {
        [0] {
          env {
            new {
              name = "CONFIGMAP_HASH"
              value = "\(mcProxy.velocity.output.text.sha1)"
            }
          }
          ports {
            [0] {
              hostPort = 25565
            }
          }
        }
      }
    }
  }
}

resources {
  new Namespace {
    metadata {
      name = "minecraft"
      labels {
        ["pod-security.kubernetes.io/enforce"] = "privileged"
      }
    }
  }
  ...mcProxy.resources
  ...mcServer.resources
}
