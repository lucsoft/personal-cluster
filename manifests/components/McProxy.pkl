module itzg.docker.McProxy

import "@k8s/api/core/v1/ConfigMap.pkl"
import "@k8s/api/core/v1/PersistentVolumeClaim.pkl"
import "@k8s/api/core/v1/PodSpec.pkl"
import "@k8s/K8sResource.pkl"

import "../components/Velocity.pkl"
import "../components/Workload.pkl"

type: "BUNGEECORD" | "WATERFALL" | "VELOCITY" | "CUSTOM" = "VELOCITY"
memory: String = "512M"
iconUrl: String? = null
jvmOps: Listing<String> = new {}
pluginUrls: Listing<String> = new {}
velocity: Velocity = new Velocity {
  forcedHosts {
    when (autoForcedHosts) {
      for (serverName, address in velocity.servers.list.toMap()) {
        ["\(serverName).\(hostname!!)"] {
          serverName
        }
      }
    }
  }
}
hostname: String? = null
autoForcedHosts: Boolean = true

local moduleThis = this

fixed podSpec = new PodSpec {
  securityContext {
    runAsUser = 1000
    runAsGroup = 1000
    fsGroup = 1000
    seccompProfile {
      type = "RuntimeDefault"
    }
  }
  containers {
    new {
      name = "mc-proxy"
      image = "docker.io/itzg/mc-proxy"
      resources {
        limits {
          ["memory"] = moduleThis.memory
        }
        requests {
          ["memory"] = moduleThis.memory
        }
      }
      securityContext {
        allowPrivilegeEscalation = false
        runAsNonRoot = true
      }
      ports {
        new {
          name = "mc-proxy"
          containerPort = 25565
        }
      }
      env {
        new {
          name = "TYPE"
          value = type
        }
        new {
          name = "MEMORY"
          value = memory
        }
        when (iconUrl != null) {
          new {
            name = "ICON"
            value = iconUrl
          }
        }
        when (jvmOps.length > 0) {
          new {
            name = "JVM_OPS"
            value = jvmOps.join(" ")
          }
        }
        when (pluginUrls.length > 0) {
          new {
            name = "PLUGINS"
            value = pluginUrls.join(",")
          }
        }
      }
    }
  }
}

class ConfigFile {
  path: String
  content: String
}

configFiles: Listing<ConfigFile> = new {
  when (type == "VELOCITY") {
    new ConfigFile {
      path = "/config/velocity.toml"
      content = velocity.output.text
    }
    new ConfigFile {
      path = "/config/forwarding.secret"
      content = workload.name.sha1
    }
  }
}

workload: Workload = new {
  name = "minecraft-proxy"
  spec = (podSpec) {
    volumes {
      new {
        name = "storage"
        persistentVolumeClaim {
          claimName = "\(workload.name)-data"
        }
      }
      new {
        name = "config"
        configMap {
          name = "\(workload.name)-config"
        }
      }
    }
    containers {
      [0] {
        volumeMounts {
          new {
            name = "storage"
            mountPath = "/server"
          }
          for (configFile in configFiles) {
            new {
              name = "config"
              mountPath = configFile.path
              subPath = configFile.path.sha1
            }
          }
        }
      }
    }
  }
}

resources: Listing<K8sResource> = new {
  ...workload.resources
  new ConfigMap {
    metadata {
      name = "\(workload.name)-config"
      namespace = workload.namespace
    }
    data {
      for (file in configFiles) {
        [file.path.sha1] = file.content
      }
    }
  }
  new PersistentVolumeClaim {
    metadata {
      name = "\(workload.name)-data"
      namespace = workload.namespace
    }
    spec {
      accessModes {
        "ReadWriteOnce"
      }
      resources {
        requests {
          ["storage"] = "1Gi"
        }
      }
    }
  }
}
